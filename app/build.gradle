/*
 * Copyright 2014-2017 Julien Guerinet
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Set up the signing and private properties
def signingProps = new Properties()
if (rootProject.file("util/signing.properties").exists()) {
    signingProps.load(new FileInputStream(rootProject.file("util/signing.properties")))
}
def privateProps = new Properties()
if (rootProject.file("util/private.properties").exists()) {
    privateProps.load(new FileInputStream(rootProject.file("util/private.properties")))
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'me.tatarka.retrolambda'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}
android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.guerinet.mymartlet"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 3
        versionName "1.0.1"
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('../util/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file(signingProps.storeFile ?: 'N/A')
            storePassword signingProps.storePassword ?: 'N/A'
            keyAlias signingProps.keyAlias ?: 'N/A'
            keyPassword signingProps.keyPassword ?: 'N/A'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix '.' + defaultConfig.versionCode
            buildConfigField 'boolean', 'REPORT_CRASHES', 'false'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'REPORT_CRASHES', 'true'
        }

        beta {
            initWith release
            versionNameSuffix '.' + defaultConfig.versionCode
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError true
        warning 'MissingTranslation'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "MyMartlet-${variant.buildType.name}.apk")
        }
    }
}

// Set the Facebook/Twitter keys from the private properties file (null if not defined)
android.buildTypes.each { type ->
    type.buildConfigField 'String', 'TWITTER_KEY', privateProps.twitterKey ?: '"N/A"'
    type.buildConfigField 'String', 'TWITTER_SECRET', privateProps.twitterSecret ?: '"N/A"'
    type.resValue 'string', 'facebook_app_id', privateProps.facebookAppId ?: '"N/A"'
    type.resValue 'string', 'facebook_provider', 'com.facebook.app.FacebookContentProvider' +
            privateProps.facebookAppId ?: '"N/A'
}

// Versions for libraries with multiple dependencies
def butterknife_version = "8.5.1"
def dagger_version = "2.10"
def dbflow_version = "4.0.0-beta5"
def okhttp_version = "3.7.0"
def play_services_version = "11.0.4"
def retrofit_version = "2.2.0"
def suitcase_version = "2.1.1"
def support_version = "25.3.1"

dependencies {
    compile "com.android.support:appcompat-v7:${support_version}"
    compile "com.android.support:customtabs:${support_version}"
    compile "com.android.support:cardview-v7:${support_version}"
    compile "com.android.support:design:${support_version}"
    compile 'com.android.support:multidex:1.0.1'
    compile "com.android.support:recyclerview-v7:${support_version}"
    compile "com.android.support:support-v4:${support_version}"
    compile("com.crashlytics.sdk.android:crashlytics:2.6.7@aar") {
        transitive = true;
    }
    compile "com.facebook.android:facebook-android-sdk:4.22.0"
    compile "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
    compile "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    compile "com.google.android.gms:play-services-maps:${play_services_version}"
    compile "com.google.dagger:dagger:${dagger_version}"
    kapt "com.google.dagger:dagger-compiler:${dagger_version}"
    provided "org.glassfish:javax.annotation:10.0-b28"
    compile "com.guerinet:form-generator:3.2.2"
    compile "com.guerinet.suitcase:analytics:${suitcase_version}"
    compile "com.guerinet.suitcase:date:${suitcase_version}"
    compile "com.guerinet.suitcase:dialog:${suitcase_version}"
    compile "com.guerinet.suitcase:io:${suitcase_version}"
    compile "com.guerinet.suitcase:log:${suitcase_version}"
    compile "com.guerinet.suitcase:prefs:${suitcase_version}"
    compile "com.guerinet.suitcase:ui:${suitcase_version}"
    compile "com.guerinet.suitcase:util:${suitcase_version}"
    compile "com.jakewharton:butterknife:${butterknife_version}"
    kapt "com.jakewharton:butterknife-compiler:${butterknife_version}"
    compile "com.jakewharton.threetenabp:threetenabp:1.0.5"
    compile "com.jakewharton.timber:timber:4.5.1"
    compile "com.orhanobut:hawk:2.0.1"
    compile "com.squareup.moshi:moshi:1.4.0"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttp_version}"
    compile "com.squareup.okhttp3:okhttp:${okhttp_version}"
    compile "com.squareup.okio:okio:1.12.0"
    compile "com.squareup.picasso:picasso:2.5.2"
    compile "com.squareup.retrofit2:converter-moshi:${retrofit_version}"
    compile "com.squareup.retrofit2:retrofit:${retrofit_version}"
    compile("com.twitter.sdk.android:twitter:2.3.2@aar") {
        transitive = true;
    }
    compile "com.viewpagerindicator:library:2.4.1@aar"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    // Don't update this as 1.10.x breaks the converters
    compile "org.jsoup:jsoup:1.9.1"
}

apply plugin: 'com.google.gms.google-services'