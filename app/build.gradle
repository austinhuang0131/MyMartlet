/*
 * Copyright 2014-2016 Julien Guerinet
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Setting up the release keystore properties
def props = new Properties()
props.load(new FileInputStream(rootProject.file("util/signing.properties")))

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    signingConfigs {
        debug {
            storeFile file('../util/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file(props.storeFile)
            storePassword props.storePassword
            keyAlias props.keyAlias
            keyPassword props.keyPassword
        }
    }

    defaultConfig {
        applicationId "ca.appvelopers.mcgillmobile"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 23
        versionName "2.2.6"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            buildConfigField 'boolean', 'REPORT_CRASHES', 'false'
            versionNameSuffix "." + defaultConfig.versionCode
        }

        beta {
            initWith debug
            signingConfig signingConfigs.release
            buildConfigField 'boolean', 'REPORT_CRASHES', 'true'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'REPORT_CRASHES', 'true'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:support-vector-drawable:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.0@aar') {
        transitive = true;
    }
    compile 'com.facebook.android:facebook-android-sdk:4.14.0'
    compile 'com.google.android.gms:play-services-analytics:9.2.1'
    compile 'com.google.android.gms:play-services-maps:9.2.1'
    compile 'com.google.dagger:dagger:2.5'
    apt 'com.google.dagger:dagger-compiler:2.5'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.guerinet:android-utils:1.1.2'
    compile 'com.guerinet:form-generator:2.2.3'
    compile 'com.instabug.library:instabugsupport:1+'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.3'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.squareup.moshi:moshi:1.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okio:okio:1.6.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:converter-moshi:2.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.1'
    compile('com.twitter.sdk.android:twitter:1.14.1@aar') {
        transitive = true;
    }
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'org.jsoup:jsoup:1.9.1'
}

apply plugin: 'com.google.gms.google-services'