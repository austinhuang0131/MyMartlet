/*
 * Copyright 2014-2018 Julien Guerinet
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Set up the signing and private properties
def signingProps = new Properties()
if (rootProject.file("util/signing.properties").exists()) {
    signingProps.load(new FileInputStream(rootProject.file("util/signing.properties")))
}
def privateProps = new Properties()
if (rootProject.file("util/private.properties").exists()) {
    privateProps.load(new FileInputStream(rootProject.file("util/private.properties")))
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}
android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.guerinet.mymartlet"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 3
        versionName "1.0.1"
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('../util/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file(signingProps.storeFile ?: 'N/A')
            storePassword signingProps.storePassword ?: 'N/A'
            keyAlias signingProps.keyAlias ?: 'N/A'
            keyPassword signingProps.keyPassword ?: 'N/A'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix '.' + defaultConfig.versionCode
            buildConfigField 'boolean', 'REPORT_CRASHES', 'false'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'REPORT_CRASHES', 'true'
        }

        beta {
            initWith release
            versionNameSuffix '.' + defaultConfig.versionCode
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError true
        warning 'MissingTranslation'
    }

//    applicationVariants.all { variant ->
//        variant.outputs.each { output ->
//            output.outputFile = new File(output.outputFile.parent,
//                    "MyMartlet-${variant.buildType.name}.apk")
//        }
//    }
}

// Set the Facebook/Twitter keys from the private properties file (null if not defined)
android.buildTypes.each { type ->
    type.buildConfigField 'String', 'TWITTER_KEY', privateProps.twitterKey ?: '"N/A"'
    type.buildConfigField 'String', 'TWITTER_SECRET', privateProps.twitterSecret ?: '"N/A"'
    type.resValue 'string', 'facebook_app_id', privateProps.facebookAppId ?: '"N/A"'
    type.resValue 'string', 'facebook_provider', 'com.facebook.app.FacebookContentProvider' +
            privateProps.facebookAppId ?: '"N/A'
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

// Versions for libraries with multiple dependencies
def butterknife_version = "8.5.1"
def dbflow_version = "4.2.4"
def koin_version = "0.9.2"
def okhttp_version = "3.10.0"
def play_services_version = "15.0.0"
def retrofit_version = "2.4.0"
def room_version = "1.1.1"
def suitcase_version = "3.1.1"
def support_version = "27.1.1"

dependencies {
    implementation "android.arch.persistence.room:runtime:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"
    implementation 'androidx.core:core-ktx:0.3'
    implementation "com.afollestad.material-dialogs:commons:0.9.6.0"
    implementation "com.android.support:appcompat-v7:${support_version}"
    implementation "com.android.support:customtabs:${support_version}"
    implementation "com.android.support:cardview-v7:${support_version}"
    implementation "com.android.support:design:${support_version}"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:recyclerview-v7:${support_version}"
    implementation "com.android.support:support-v4:${support_version}"
    implementation("com.crashlytics.sdk.android:crashlytics:2.9.1@aar") {
        transitive = true
    }
    implementation "com.facebook.android:facebook-android-sdk:4.32.0"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:${dbflow_version}"
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    implementation "com.google.android.gms:play-services-maps:${play_services_version}"
    implementation "com.guerinet:morf:5.0.0"
    implementation "com.guerinet.suitcase:analytics:${suitcase_version}"
    implementation "com.guerinet.suitcase:date:${suitcase_version}"
    implementation "com.guerinet.suitcase:dialog:${suitcase_version}"
    implementation "com.guerinet.suitcase:io:${suitcase_version}"
    implementation "com.guerinet.suitcase:log:${suitcase_version}"
    implementation "com.guerinet.suitcase:prefs:${suitcase_version}"
    implementation "com.guerinet.suitcase:ui:${suitcase_version}"
    implementation "com.guerinet.suitcase:util:${suitcase_version}"
    implementation "com.jakewharton:butterknife:${butterknife_version}"
    kapt "com.jakewharton:butterknife-compiler:${butterknife_version}"
    implementation "com.jakewharton.threetenabp:threetenabp:1.0.5"
    implementation "com.jakewharton.timber:timber:4.7.0"
    implementation "com.orhanobut:hawk:2.0.1"
    implementation "com.squareup.moshi:moshi:1.5.0"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttp_version}"
    implementation "com.squareup.okhttp3:okhttp:${okhttp_version}"
    implementation "com.squareup.okio:okio:1.14.0"
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "com.squareup.retrofit2:converter-moshi:${retrofit_version}"
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation("com.twitter.sdk.android:twitter:3.2.0@aar") {
        transitive = true
    }
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.21"
    // Don't update this as 1.10.x breaks the converters
    implementation "org.jsoup:jsoup:1.9.1"
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
}

apply plugin: 'com.google.gms.google-services'