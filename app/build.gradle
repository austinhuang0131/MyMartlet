/*
 * Copyright 2014-2017 Julien Guerinet
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Setting up the release keystore properties
def props = new Properties()
props.load(new FileInputStream(rootProject.file("util/private.properties")))

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'me.tatarka:gradle-retrolambda:3.6.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    signingConfigs {
        debug {
            storeFile file('../util/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file(props.storeFile)
            storePassword props.storePassword
            keyAlias props.keyAlias
            keyPassword props.keyPassword
        }
    }

    defaultConfig {
        applicationId "ca.appvelopers.mcgillmobile"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 25
        versionName "2.3.2"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            buildConfigField 'boolean', 'REPORT_CRASHES', 'false'
            versionNameSuffix "." + defaultConfig.versionCode
        }

        beta {
            initWith debug
            signingConfig signingConfigs.release
            buildConfigField 'boolean', 'REPORT_CRASHES', 'true'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'REPORT_CRASHES', 'true'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'MissingTranslation'
    }
}

// Set the Twitter keys from the properties filed
android.buildTypes.each { type ->
    type.buildConfigField 'String', 'TWITTER_KEY', props.twitterKey
    type.buildConfigField 'String', 'TWITTER_SECRET', props.twitterSecret
    type.resValue 'string', 'facebook_app_id', props.facebookAppId
    type.resValue 'string', 'facebook_provider', 'com.facebook.app.FacebookContentProvider' +
            props.facebookAppId
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support:support-v4:25.3.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }
    compile 'com.facebook.android:facebook-android-sdk:4.20.0'
    compile 'com.github.Raizlabs.DBFlow:dbflow:4.0.0-beta5'
    compile 'com.github.Raizlabs.DBFlow:dbflow-core:4.0.0-beta5'
    annotationProcessor 'com.github.Raizlabs.DBFlow:dbflow-processor:4.0.0-beta5'
    compile 'com.google.android.gms:play-services-analytics:10.2.1'
    compile 'com.google.android.gms:play-services-maps:10.2.1'
    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.guerinet:android-utils:1.2.1'
    compile 'com.guerinet:form-generator:3.1.0'
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.orhanobut:hawk:2.0.1'
    compile 'com.squareup.moshi:moshi:1.4.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okio:okio:1.11.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:converter-moshi:2.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile('com.twitter.sdk.android:twitter:2.3.2@aar') {
        transitive = true;
    }
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'org.jsoup:jsoup:1.10.2'
}

apply plugin: 'com.google.gms.google-services'